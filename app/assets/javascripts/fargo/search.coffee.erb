#= require templates/fargo_result

window.humanize_bytes = (bytes) ->
  suffix = 'B'
  stop = false
  while bytes > 1024 && !stop
    bytes /= 1024
    switch suffix
      when  'B' then suffix = 'KB'
      when 'KB' then suffix = 'MB'
      when 'MB' then suffix = 'GB'
      when 'GB' then suffix = 'TB'
      when 'TB' then stop = true

  bytes = parseInt bytes * 100, 10
  bytes /= 100.0
  bytes + ' ' + suffix

jQuery ->
  channel = pusher.subscribe $('#channel').val()
  fargo_channel = pusher.subscribe 'private-fargo'

  # On search results, display nice fancy results with events bound already.
  channel.bind 'search-result', (data) ->
    rendered = $(JST['templates/fargo_result'](data))
    rendered.data('search', data)
    $('.loader').remove()

    rendered.find('.more').bind 'click', ->
      $(this).siblings('.coalesced').slideToggle()
      false
    rendered.find('.download a').bind 'click', ->
      element = $('<span>').text('Queueing...')
      data = $(this).closest('.result').data('search')
      $(this).replaceWith(element)
      fargo_channel.trigger('client-download',
        file: data.file,
        tth:  data.tth,
        nick: data.nick,
        size: data.size,
        channel: channel.name
      )
      false

    duplicate = $('.result[tth="' + data.tth + '"]')
    if duplicate.length > 0
      duplicate.children('.coalesced').append(rendered)
      count = duplicate.children('.coalesced').children().length
      duplicate.children('.more').text('+ (' + count + ')')
    else
      rendered.attr('tth', data.tth)
      $('#search-response').append(rendered)

  # When we get the uuid response from the worker, then we can create the link
  # which will send us to the status page.
  channel.bind 'job-uuid', (data) ->
    bubble = $('.result[tth="' + data.tth + '"] .download')
    bubble.html(
      $('<a>').attr('href', '/resque/statuses/' + data.uuid).
               attr('target', '_blank').
               addClass('notice').
               text('Queued!')
    )

  # Upon submission of the form, instead send the request over the websocket to
  # the worker.
  $('form').bind 'submit', ->
    $('.loader').remove()
    if ($('#q').val() == '')
      return false
    $('#search-response').children().remove()
    loader = $('<img>').attr('src', '<%= asset_path "ajax-small.gif" %>')
    loader.addClass('loader')
    $(this).find('p').append(loader)
    fargo_channel.trigger('client-search',
      channel: channel.name
      query: $('#q').val()
    )
    false
