#= require jquery-ui

jQuery ->
  slug = $('#songs').data('id')
  channel = pusher.subscribe 'playlist-' + slug

  channel.bind 'removed_item', (data) ->
    $('.song[data-queue-id=' + data.queue_id + ']').slideUp -> $(this).remove()

  channel.bind 'playing', (data) -> $('#current-song').text data.song

  channel.bind 'added_item', (data) -> $('#songs').load(data.url + ' #songs')

  channel.bind 'connected', ->
    $('.status').show()
    $('.search .playing').show()
    $('.search .not-playing').hide()

  channel.bind 'disconnected', ->
    $('.status').hide()
    $('.search .playing').hide()
    $('.search .not-playing').show()

  channel.bind 'error', (data) -> alert 'Error for playlist: ' + data.message

  pusher.connection.bind 'pusher:subscribe', (data) -> console.log data
  radio = pusher.subscribe 'private-radio'
  $('.search .start').click ->
    radio.trigger 'client-add', playlist_id: slug
    false
  $('.search .stop').click ->
    radio.trigger 'client-remove', playlist_id: slug
    false
  $('.search .next').click ->
    radio.trigger 'client-next', playlist_id: slug
    false

  $('#q').autocomplete(
    source: '/songs/search.json'
    select: (event, ui) ->
      $(this).prev('input:hidden').val(ui.item.id)
      $(this).val(ui.item.name)
      false
    focus: (event, ui) ->
      $(this).val(ui.item.name)
      false
  ).data('autocomplete')._renderItem = (ul, item) ->
    $('<li/>').data('item.autocomplete', item)
              .append('<a>' + item.image + ' ' + item.artist + ' &mdash; ' +
                              item.name + '</a>')
              .appendTo(ul)
